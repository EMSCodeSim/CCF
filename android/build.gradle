// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
        classpath 'com.google.gms:google-services:4.4.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

// Google Play Billing Library policy enforcement:
// Force BillingClient >= 7.0.0 even if a transitive dependency pulls an older version.
// (Play Console scans the final bundle for old billing artifacts.)
subprojects { proj ->
    proj.configurations.all { cfg ->
        cfg.resolutionStrategy {
            force 'com.android.billingclient:billing:7.0.0'
            force 'com.android.billingclient:billing-ktx:7.0.0'

            eachDependency { details ->
                if (details.requested.group == 'com.android.billingclient' &&
                        (details.requested.name == 'billing' || details.requested.name == 'billing-ktx')) {
                    details.useVersion '7.0.0'
                    details.because 'Play Billing policy requires 7.0.0+'
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
